# Phoenix
# Deikhan Holy Vindicator Quest
# Created by Stonecold
# Copyright 1997, SneezyMUD Development Team.  All rights reserved.

say { "hello";
	checktoggle 16;
	say Hello %n, what are you doing way up here?;
	}

say { "holy Vindicator";
	checktoggle 16;
	say Who sent you to me?;
	}

say { "a feather";
	checktoggle 16;
	say Who sent you to me?;
	}

say { "Fistandantilus";
	say Bah, go away.  Why would he send you to me?;
	checktoggle 16;
	say Oh ..... Wait!!;
	say I remember now, I had heard of your skill before...;
	say but didn't know you had gotten so powerful so fast.;
	say You are probably here for one of my feathers.;
	ponder %n;
	say Well, Fist may believe you are ready.  But I am not sure yet.; 
	say Before I give up one of my feathers willingly;
	say you must perform a task for me......;
	unflag %n;
	untoggle 16;
	toggle 17;
	}

say { a task;
	checktoggle 17;
	say In the Arden forest, there is a certain evil tree.;
	say This tree is inhabited by a tree demon.;
	say If one is properly prepared, the demon will leave the tree to meet you.;
	say Prove your strength and courage in combat with this tree demon and I will know that you are worthy.;
	say If you prevail solo,  I will reward you with a single feather.;
	say The demon is strong and does not bend easily, there is no;
	say shame in waiting until you become stronger for this task.;
	tovict $n waves $s wings in a circle over your head.;
	tonotvict $n waves $s wings in a circle over $N's head.;
	say Tell me if you are worthy and wish to undertake this task for me now.;
	say If you want to take a break you may by saying so.;
	}

say { the task;
	link say a task;
	}

say { "a break";
	checktoggle 17;
	say OK %n, when you are prepared to proceed, return to me...;
	say and ask me again about the task.;
	}

say { "i am worthy";
	checktoggle 17;
	nod;
	say I believe you just may be.;
	link package 200;
	}

package { "200";
	toroom $n sings a soft melody that echos throughout the heavens;
	toroom $n sings and you feel your heart feel glad and your soul take on a magic aura.;
	say The demon has heard my song and will meet you on the field of battle.;
	say Once you leave my sight, you can not accept help;
	say Till then you still have the option of taking a break.;
	say If you undertake this task, only return if you have killed the demon.;
	say I will only meet with you then if you tell me the good news;
	say that you have killed the demon in a battle of honor.;
	say If you die, do not come back till Fistandantilus has judged you stronger.;
	say Do not ever lie to me %n.;
	bow %n;
	say I wish you well %n and bring me back good news.;
	toroom $n sings a soft melody that echos throughout the heavens;
	toggle 18;
	untoggle 17;
	flag %n;
	}

say { undertake this task;
	checktoggle 17;
	link package 200;
	}

say { "take a break";
	checktoggle 18;
	untoggle 18;
	link package 555;
	}	

# loser wants to take a break when he already won?
say { "take a break";
	checktoggle 19;
	untoggle 19;
	link package 555;
	}	

package { "555";
	say Ok, come remind me again when you are ready.;
	say I will review my task for you again then.;
	unflag %n;
	toggle 17;
	}

# At one point, this resulted in "cheat2" flag being set.
# seems too harsh though
say { "killed the demon";
	checktoggle 18;
 	tovict $n examines you and your soul reveals your deceit.; 
	tonotvict $n examines %n carefully.;
	growl %n;
	say You lie!  The demon still lives!;
	say Now Begone! Till you have done this, do not trouble my presence.; 
	}

say { "killed the demon";
	checktoggle 19;
	tovict $n looks into the skies, searching for something.;
	tonotvict $n looks into the skies momentarily.;
	smile %n;
	say Yes, you have bested the demon and earned my feather.;
	toroom $n picks a feather from the widest part of $s wing.;
	load 29021;
	give fiery %n;
	untoggle 19;
	toggle 21;
	unflag %n;
	bow %n;
	say Go now and leave me to my solitude...;
	}

say { "killed the demon";
	checktoggle 20;
	tovict $n examines your soul.;
	tonotvict $n examines your %n's soul.;
	growl %n;
	say You lie!  You did not kill the demon honorably.;
	say Go back to Fistandantilus...;
	say and tell him you have dishonored yourself...;
	say and need purification.;
	say Now Begone!;
	say Till you have done this, do not trouble my presence.;
	toggle 24;
	untoggle 20;
	}
