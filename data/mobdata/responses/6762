# Yurno
# Trainer - Shaman Alchemy L60
# Created by Stonecold
# Copyright 1997, by SneezyMUD Development Team.  All rights reserved.

# Directions to L100 Shaman Alchemy Trainer (Halendos, mob 6813)

# added lycanthropy quest (Maror, 2003)

say { "hello";
	say Hello %n, what can I help you with?;
	}

say { "next trainer";
	tell %n Finding your next trainer is part of training;
	tell %n However, I can give you a hint......;
	tell %n Seek thee one who will allow entrance;
	tell %n In the mage guild of Grimhaven.;
	tell %n The price of gaining entrance is fair.;
	tell %n It's the ticket you need that will send you there.;
	tell %n But woe unto you if you go there alone.;
	}

package { "1";
  checktoggle 266;
  peer %n;
  say Ah, how unfortunate.  I suppose I should take some time out of my busy schedule...;
  stare slot;
  say Of course, you would have to provide the <g>ingredients<z>.;
  toggle 270;
}

package { "2";
  checkuntoggle 266;
  say Can't you see that I am very busy?;
  smile slot;
}

package { "3";
  checkuntoggle 271;
  say I will need a flask of urine from a pure woman (unfortunately rare in these days of sacrifice).  You should use one of those flasks of elixir.;
}

package { "4";
  checkuntoggle 272;
  say I will need a sprig of wolvesbane.;
}

package { "5";
  checkuntoggle 274;
  say I will need some poisonous hemlock, extract will do.;
}

package { "6";
  checkuntoggle 273;
  say I will need a bit of silver.;
}

package { "7";
  checktoggle 271;
  checktoggle 272;
  checktoggle 273;
  checktoggle 274;
  untoggle 271;
  untoggle 272;
  untoggle 273;
  untoggle 274;
  untoggle 270;
  say It looks like you've collected everything we'll need!;
  say Let's get to work!;
  toroom Yurno mixes the ingredients into the flask of urine.;
  toroom Yurno digs in his pockets.;
  toroom Yurno beams with pleasure as he pulls out a glob of something brown and adds it to the mix.;
  say Perfect!;
  toroom Yurno reaches into the flask and pulls out a sticky mess.;
  load 31023;
  give glop %n;
}

say { "lycanthropy";
  link package 1;
  link package 2;
}

say { "lycanthrope";
  link package 1;
  link package 2;
}

say { "werewolf";
  link package 1;
  link package 2;
}

say { "ingredients";
  checktoggle 270;
  say Let's see now...;
  link package 3;
  link package 4;
  link package 5;
  link package 6;
  link package 7;
}

say { "wolvesbane";
  checktoggle 270;
  checkuntoggle 272;
  say I believe there is a druid in the forest of Arden who uses it in her work with animal healing.;
}

give { "31021";
  checktoggle 270;
  toroom Yurno takes a small sip from the flask.;
  say Fantastic!;
  toggle 271;
  link package 7;
}

give { "31022";
  checktoggle 270;
  toroom Yurno places the <g>sprig<z> against his tongue.;
  say That should do nicely.;
  toggle 272;
  link package 7;
}

give { "51";
  checktoggle 270;
  toroom Yurno looks closely at the silver.;
  say Ah, yes.;
  toggle 273;
  link package 7;
}

give { "31011";
  checktoggle 270;
  toroom Yurno sticks his finger in the vial and sniffs it.;
  sneeze;
  say I think this SHOULD be enough, we shall see.;
  toggle 274;
  link package 7;
}


