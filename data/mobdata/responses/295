# Sword Swallower
# Script by Ragamuffin, Script Competition
# Copyright 2004 by SneezyMUD Development Team

pulse { "";
        random 20;
        randoption 1;
    whistle;
        randoption 2;
    say Would any of you peasants wish to see an amazing performance?;
        randoption 3;
    toroom $n picks $m fingernails daintily, looking bored.;
        randoption 4;
    yawn;
        randoption 5;
        randoption 6;
        randoption 7;
        randoption 8;
        randoption 9;
        randoption 10;
        randoption 11;
        randoption 12;
        randoption 13;
        randoption 14;
        randoption 15;
        randoption 16;
        randoption 17;
        randoption 18;
        randoption 19;
        randoption 20;
        donerand;
      }

say { "hello";
      tell %n I don't have time for idle chit chat with commoners.  If you want to see my performance, ask me about it.;
    }

say { "perform";
      tell %n Well, although my amazing talent is wasted on the likes of you, I'll give you a chance.;
      tell %n Bring me a wooden weapon, suitable for training, and I'll put on a show.;
      tap;
      tell %n And don't keep me waiting.;
    }

say { "preform";
      tell %n That's not how you spell 'perform' you dolt.;
      roll;
    }

package { "1";
    say So, you want to see me swallow this sword?;
    say Prepare to be amazed!  Astounded!  Enthralled!;
    tonotvict $n gives %n a sky wink.;
    tovict $n winks at you slyly.  Maybe $e knows a secret?;
    say Stand back!;
          random 2;
          randoption 1;
      toroom $n leans $m head back and holds the sword high in the air.;
      toroom Slowly, the sword begins to fall toward $n's open mouth.;
      toroom With a strange gulping sound, $n swallows the sword whole!;
          randoption 2;
      toroom $n leans $m head back and throws the sword high into the air.;
      toroom The sword spins and tumbles as it slowly falls back toward the earth.;
      toroom $n leaps straight upward and the sword disappears into $m open mouth!;
        donerand;
    bow;
    say No autographs, please.;
        }

give { "329";
  link package 1;
     }

give { "334";
  link package 1;
     }

package { "2";
    say What?  You idiot!  This is a staff, not a sword!  I swallow swords!;
    bonk %n;
    toroom $n snaps the staff over $m knee.;
    say Come back when you can tell the difference, you silly oaf.;
        }

give { "177";
  link package 2;
     }
give { "324";
  link package 2;
     }

give { "325";
  say This is a dagger!  I swallow swords, not these little things.;
  sigh;
  say Okay, just this once...;
  toroom With a quick snapping motion, $n swallows the dagger in one gulp!;
  load 182;
  burp;
  give toothpick %n;
  bow;
  say No autographs, please.;
     }
