# Bulge the Ogre
# Toll collector mob at Great Eastern Crossroads
# Created by Moath - June 06 1998
# Modified by Dolgan - June 07 1998
# Copyright 1998, SneezyMUD development team.  All rights reserved.

roomenter { "";
	tonotvict $n says something to %n.;
	tell %n Hold it scum!;
	tell %n No one gets by without paying me a toll.;
	tell %n Ever since the drow have been raiding this road...;
	tell %n Logrus has been forced to guard it.;
	tell %n In return for that service you must pay a toll of 500 talens.; 
	checktoggle 129;
	untoggle 129;
	}

say { "scum";
	tonotvict $n sneers at %n.;
	tovict $n sneers at you contemptuously.;
	tonotvict $n says something to %n.;
	tell %n You heard me scum!;
	tell %n Want to make something of it??;
	}

say { "toll";
	toroom $n smiles greedily.;
	tonotvict $n says something to %n.;
	tell %n If you want to get past me...;
	tell %n You’re going to have to fork over 500 talens.;
	tell %n Or you can wait till nightfall...;
	tell %n and take a chance with the drow.;
	}

say { "pay";
	link say toll;
	}

say { "talens";
	link say toll;
	}

say { "past";
	toroom $n sighs.;
	say Yeah I can't stop you from going back the way you came...;
	say But don't even try and get past me without paying!;
	}

say { "drow";
	toroom $n looks furtively about.;
	say There have been drow raiders attacking the roads lately.;
	say The city of Logrus has managed to keep them at bay during the day...;
	say But it has proven to be too costly to guard the roads at night.;
	say Only a fool would travel these roads at night!;
	}

say { "Logrus";
	link say drow;
	}

say { "guard";
	link say drow;
	}

say { "road";
	tonotvict $n whispers to %n.;
	tovict $n leans over and whispers to you.;
	tell %n Well I'm actually only supposed to charge...;
	tell %n for using the Redstone Road...;
	tell %n but I happen to like my job...;
	tell %n so I just charge for every road.;
	toroom $n giggles uncontrollably.;
	}

say { "service";
	toroom $n attempts a half-hearted attempt at looking offended.;
	say Of course it's a service!;
	say You think it is cheap...;
	say to maintain a military presence on this road??;
	grin %n;
	}

give { "-500";
	smile %n.;
	say Thank you for your cooperation.;
	say Have a nice day!;
	say And remember: We are here for your protection!;
	toggle 129;
	}

say { "protection";
	tonotvict $n growls at %n.;
	tovict $n growls at you.;
	say Well if you think we are not needed...;
	say Try coming at night!;
	grin %n;
	}

say { "Moath";
	toroom $n drops to one knee in reverence.;
	say Moath is the master!;
	say Hail to the arch-mage.;
	}

