# Fistandantilus
# Deikhan Vindicator Quest
# Created by Stonecold 
# Copyright 1997, SneezyMUD Development Team.  All rights reserved.

say { "hello";
	say Hello %n;
	say Your travels have brought you far afield.;
	}

say { "holy Vindicator";
	checktoggle 9;
	untoggle 9;
	toggle 10;
	link package 100;
	}

package { "100";
	say So %n, your guildmaster has sent you out to seek a holy Vindicator?;
	say Alas, I lack the <r>ingredients<z> necessary to construct one right now.;
	}

say { "holy Vindicator";
	checktoggle 10;
	link package 100;
	}

say { "ingredients";
	checktoggle 10;
	toroom $n wipes his sweaty brow;
	say Well, the only way I can make such a fine weapon...;
	say is with certain magic ingredients.;
	say I am afraid they are not something I can keep on hand.;
	say First you must bring me a special metal ore.;
	say Then you must bring me the magic binding ingredient.;
	say And you must do it alone without help or the magic fails.;
	say I will tell you all about it...;
	say before you decide to undertake either task.;
	say If you understand what I am telling you so far...;
	say say you understand;
	}

say { "i understand";
	checktoggle 10;
	say Very well %n, we shall proceed.;
	say There are two tasks you must do.;
	say I will tell you what the first one is now.;
	say Far below The World's crust lies the Temple of Ryoken;
	say Deep within its darkened caverns you will find...;
	say one who possesses some red ore.;
	say This ore has special properties if baptized in blood.;
	say You must kill the bearer of this ore in a test of honor.;
	say You must stand with him and stain the ore with his blood.;
	say He will tell you if the fight is with honor or not.;
	say If you are able to defeat him without flight or help.;
	say Bring me that part of the ore that has touched his blood.;
	say Leave any ore that has not been blessed.;
	say Then, I will send you on the second task.;
	say When you have understood so far tell me to <r>please continue<z>;
	}

say { "please continue";
	checktoggle 10;
	say Now, once again you must do all this task by yourself.;
	say Only then will you be truly worthy of a holy weapon.;
	say You may accept no outside assistance in fighting the captain.;
	say although it is possible that you may need help finding him.;
	say While you are on this quest...;
	say You will not be able to interact with other mortals...;
	say Save through conversation.;
	say Once you have the ore, return here and give it to me...;
	say If you should die during the quest, you must start again.;
	say I will give you time to think on this.;
	say There is no shame in delaying this quest until you are stronger.;
	say Whenever you want, let me know you are <r>ready to begin<z>.;
	say You need not begin immediately.;
	say You may wish to take <r>a break<z>.;
	}

say { "a break";
	checktoggle 10;
	say All right %n, just come back and let me know when you are <r>ready to begin<z>.;
	}

say { "ready to begin";
	checktoggle 10;
	nod;
	tovict $n lays $s hands upon your head and begins chanting softly.;
	tonotvict $n lays $s hands upon %n's head and begins chanting softly.;
	tovict A pale green aura appears around you, but quickly fades away.;
	tonotvict A pale green aura appears around %n, but quickly fades away.;
	tovict $n lifts $s hands from your head.;
	tonotvict $n lifts $s hands from %n's head.;
	say The Journey has begun.  Remember, if you do receive;
	say help of any kind your trials will all be for naught.;  
	say Go now and find your destiny.;
	say When you return tell me that you have killed the captain...;
	say or give me the ore.;
	say Lastly, if the task is too arduous for you, you may tell;
	say me that you need <r>a break<z> and you may begin again at a later date.;
	flag %n;
	untoggle 10;
	toggle 11;
	}

say { "a break";
	checktoggle 11;
	say Ok, come back when you have the energy to begin the first task.;
	laugh;
	say Just tell me you are <r>ready to begin<z> when you return.;
	tovict $n puts his hands on your head.;
	tovict $n puts his hands on %n's head.;
	unflag %n;
	untoggle 11;
	toggle 10;
	}

say { "killed the captain";
	checktoggle 11;
	say Bah.  Do not lie to me.  The captain still lives.;
	}

give { "29020";
	checktoggle 12;
	toroom $n examines the ore.;
	say This appears to be the right ore.;
	tovict $n places his hands on your head.;
	tonotvict $n places his hands on %n's head.;
	say Are you a Deikhan deserving of the second task?;
	say If so, have you been faithful to your vows in getting this ore?;
	say If you have failed, now is the time to say so.;
	unflag %n;
	untoggle 12;
	toggle 14;
	}

say { "killed the captain";
	checktoggle 12;
	say Good for you, just hand me the ore.;
	}

# Loser completed part#1, but isn't aware of it....
say { "a break";
	checktoggle 12;
	say Ok, come back when you have the energy to begin the first task.;
	laugh;
	say Just tell me you are <r>ready to begin<z> when you return.;
	tovict $n puts his hands on your head.;
	tovict $n puts his hands on %n's head.;
	unflag %n;
	untoggle 12;
	toggle 10;
	}

# Loser cheated and wants a break!
say { "a break";
	checktoggle 13;
	tovict $n looks at you momentarily.;
	tonotvict $n looks at $N momentarily.;
	say I can sense the <r>dishonor<z> on your virtue from where I stand.;
	say I will have nothing to do with you...;
	say until you purify yourself.;
	}

# Everything seems OK, but PC confessing he cheated anyway...
say { "i have failed";
	checktoggle 14;
	say Well at least you haven't lied about it.;
	say You still need to start again.;
	tovict $n places his hands on your head.;
	tonotvict $n places his hands on %n's head.;
	say Come back when you are <r>ready to begin<z> again.;
	untoggle 14;
	toggle 10;
	}

say { "been faithful";
	checktoggle 14;
	tovict $n places his hands on your head.;
	tonotvict $n places his hands on %n's head.;
 	tovict $n gazes fiercely into your eyes.;
	tonotvict $n gazes fiercely into $n's eyes.;
	say Yes, I can see that you have been faithful to your vows too.;
	say Are you ready to <r>hear about the second task<z>...;
	say or do you wish to <r>take a break<z>?;
	untoggle 14;
	toggle 15;
	}

say { "a break";
	checktoggle 14;
	say Take a break now?!?;
	say I asked you a question: Have you been faithful to your vows,;
	say or did you fail?;
	}

say { hear about the second task;
 	checktoggle 15;
 	say For your second task, you must find the great Phoenix;
	say Tell the Phoenix that I have sent you.;
	say Tell him then you have come for a single feather;
	say which I need to prepare my furnace with.;
	say If he gives it to you, bring it back to me.;
	say With this feather and the ore;
	say I will be able to forge you the sword.;
	say The same rules will apply as with your first task.;
	say This time you should not take any physical help finding her.;
	say although as before you are allowed to ask others for advice.;
	say If you want to <r>take a break<z> or do it later, it is ok with me.;
	say although you will have to see me to begin the second task again;
	say If you are ready to <r>begin the second task<z>, just say that.;
	}

say { "take a break";
	checktoggle 15;
	say OK by me, just come back and let me know...;
	say when you are ready to <r>hear about the second task<z>.;
	}

say { hear the second task;
 	link say hear about the second task;
	}

say { begin the second task;
	checktoggle 15;
 	say So you are ready for your second task.;
	say Remember the same rules will apply as with your first task.;
 	say I wish you luck but fear that this task maybe harder.;
 	say The Phoenix is a demanding beast sometimes.;
	say In any event, I wish you luck and godspeed.;
	say Return with the news of whether you have succeeded or failed.;
	tovict $n puts his hands on your head.;
 	tovict $n puts his hands on %n's head.;
	flag %n;
 	toggle 16;
 	untoggle 15;
	}

say { "take a break";
	checktoggle 16;
	say Yes, I have heard the Phoenix can be hard to find.;
	say When you are ready to begin looking again, just let me know...;
	say that you want to <r>begin the second task.<z>;
	unflag %n;
	toggle 15;
	untoggle 16;
	}

say { "killed the phoenix";
	checktoggle 16;
	say Bah!  I told you to talk to the Phoenix not kill it.;
	}

say { "have the feather";
	checktoggle 17;
	link package 300;
	}

say { "have the feather";
	checktoggle 18;
	link package 300;
	}

say { "have the feather";
	checktoggle 19;
	link package 300;
	}

# cheated to have toggle, lying too
say { "have the feather";
	checktoggle 20;
	tovict $n looks at you momentarily.;
	tonotvict $n looks at $N momentarily.;
	say I can sense the <r>dishonor<z> on your virtue from where I stand.;
	say I will have nothing to do with you...;
	say until you purify yourself.;
	}

package { "300";
	say If you have been given the feather...;
	say and have acted as an honorable deikhan.;
	say Then you may give it to me.;
	say If not tell me that you failed...;
	say and I will think about sending you out again.;
	}

say { "i have failed";
	checktoggle 18;
	untoggle 18;
	link package 400;
	}

say { "i have failed";
	checktoggle 19;
	untoggle 19;
	link package 400;
	}

# Lost the feather perhaps...
say { "i have failed";
	checktoggle 21;
	untoggle 21;
	link package 400;
	}

package { "400";
	toggle 10;
	say You have indeed failed in your quest.;
	say But, at least you haven't lied about it.;
	tovict $n places his hands on your head.;
	tonotvict $n places his hands on %n's head.;
	say Come back when you are ready to begin again from the beginning.;
	}

give { "29021";
	checktoggle 21;
	toroom $n examines the feather.;
	say This appears to be the right feather.;
	tovict $n places his hands on your head.;
	tonotvict $n places his hands on %n's head.;
	say Are you a deikhan deserving of a Holy Vindicator?;
	say If so, have you been faithful to your vows in getting this feather?;
	say If you have failed, now is the time to say so.;
	untoggle 21;
	toggle 22;
	}

say { "i have failed";
	checktoggle 22;
	untoggle 22;
	link package 400;
	}

say { "been faithful";
	checktoggle 22;
	tovict $n then turns towards you and looks in your eye.;
	tonotvict $n turns toward $N and examines $M.;
	tovict $n puts his hands on your head.;
	tonotvict $n puts his hands on $N's head.;
	bow %n;
	say You have done well %n! I will begin forging your sword;
	toroom $n lights a feather and waves it over his forge.;
	toroom $n stokes his furnace till it lights the skies.;
	toroom $n pounds his mighty hammer and begins forging a sword.;
	toroom Hours pass like minutes, seconds like eternities.;
	toroom Like in a dream, a Holy Vindicator takes shape in $n's hands till finally $s smiles.;
	personalize 326;
	say There you are %n, a Holy Vindicator just for you;
	say All I ask is that you keep this quest amongst the Deikhans.;
	toggle 23;
	untoggle 22;
	}
      
say { "been faithful";
	checktoggle 23;
	tovict $n gazes fiercely into your eyes.;
	tonotvict $n gazes fiercely into $n's eyes.;
	say %n, You have already been given a Holy Vindicator.;
	say Now begone ... before I kill you.;
	}

# Just dumb filler, should have no effect on quest
give { "4847";
	tovict $n examines a feather carefully and then turns towards you.;
	tonotvict $n examines a feather carefully and then turns towards %n.;
	toroom $n junks a feather.;
	say Bah!! Worthless Junk!;
	glare %n;
	say It's not even the right one!;
	say You are probably not even a Deikhan.;
	}

# More filler
give { "29022";
	tovict $n examines some ore carefully and then turns towards you.;
	tonotvict $n examines some ore carefully and then turns towards %n.;
	say Nope, this ore has not been blessed by his blood.;
	say This is the wrong one.;
	toroom $n junks some ore.;
	say Worthless Junk!;
	glare %n;
	}

# Penance pathways
say { "killed the captain";
 	checktoggle 13;
	say You have dishonored yourself totally;
	say Once in your conduct in the field, and the other in lying to me.;
	say I should kill you for your insolence...;
	say but I think I would rather that you live with dishonor.;
	say You are now outcast!;
	tovict $n places his hands on your head.;
	tonotvict $n places his hands on %n's head.;
	say Come back when you have purified yourself.;
	toggle 24;
	untoggle 13;
	}

say { "dishonor";
	checktoggle 13;
	say So %n, you have dishonored yourself in seeking honor.;
	say I should just say good bye to you...;
	say but I will give you a chance to purify yourself.;
	untoggle 13;
	toggle 24;
	}

say { "dishonor";
	checktoggle 20;
	say So %n, you have dishonored yourself with the Phoenix.;
	say I should just say good bye to you...;
	say but I will give you a chance to purify yourself.;
	untoggle 20;
	toggle 24;
	}

say { "purify";
	checktoggle 24;
	say There is a dark stain on your honor.;
	say To cleanse this stain, you must perform a task of honor.;
	say Find Iris in the clouds and tell her I sent you.;
	say Maybe by service to her you can regain your honor.;
	}

say { "purified";
	checktoggle 27;
	say It looks like you have made up for your previous dishonor.;
	tovict $n puts his hands on your head.;
	tonotvict $n puts his hands on %n's head.;
	say I am now willing to make your Holy Vindicator.;
	say But you will have to start the tasks again.;
	say When you want to begin again, let me know.;
	unflag %n;
	untoggle 27;
	toggle 10;
	}
