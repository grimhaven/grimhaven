# Bullywug Shaman
# Ranger Quest Lvl 21
# Created by Mithros
# Copyright 2000, SneezyMUD Development Team.  All rights reserved.


say {"hello";
      checktoggle 209;
      frown;
      say Me busy, leave lone.;
}

say {"hi";
      link say hello;
}

say {"oil";
      checktoggle 209;
      grumble;
      say Yes me make very nice oil, good for many things.;
      say But too busy right now to make you any.;
      say Must get staff back before chief get mad.;
}

say {"chief";
      checktoggle 209;
      snicker;
      say Chief dumb overgrown toad, but he the chief.;
      shrug;
      say Him be real mad when he see I not have my staff.;
}

say {"staff";
      checktoggle 209;
      sigh;
      say Yes, me have fancy feathered ceremonial staff.;
      say Supposed to use on special occasions...:;
      say ...to make chief look all powerful.;
      toroom $n whispers, "Don't tell chief, but staff do squat.";
      snicker;
      say But anyways, bandits steal staff.;
      say And I need to figure out how to get back.;
}

say {"bandits";
      checktoggle 209;
      growl;
      say Yes dumb bandits come in and steal staff while I away.;
      say Now they hide in swamp and torment poor shaman.;
      say Their leader is a bandit named Venol.;
      say It is he who has my staff.;
      say You want to help?  Me make you oil if you <W>will get staff<1>.;
}

say {"will get staff";
      checktoggle 209;
      smile;
      say Deal!  You get staff, me make you oil!;
      untoggle 209;
      toggle 210;
      wave;
}
 
say {"bandits";
      checktoggle 210;
      frown;
      say You not bright one eh?;
      say Me last see bandits near southwest part of beautiful swamp.;
      say Venol and his two lackeys.  Venol was flaunting my staff!;
      say Now go go! Before Chief come to visit.;
}

give {"4304";
       checktoggle 210;
       bounce;
       say Chief not hurt me!  Chief not hurt me!;
       smile %n;
       load 4309;
       give oil-skin %n;
       untoggle 210;
       toggle 211;
       thank %n;
       say You go now, me get ready for Chief.;
       wave;
}







